apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        multiDexEnabled true
        applicationId 'com.duongame.basicplayer'
        buildConfigField "boolean", "SHOW_AD", "true"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 15
        versionName "1.1.3"

        buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-16\""
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    sourceSets {
        main {
            // eclipse의 폴더명과 동일하게 사용함
            jniLibs.srcDirs = ["src/main/libs"]

            // android studio가 직접 컴파일하지 않게 함
            jni.srcDirs = []
        }
    }
    signingConfigs {
        release {
            storeFile file("../Mobicle2.keystore")
            storePassword "windbros"
            keyAlias "mobicle"
            keyPassword "windbros"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions("edition", "mode")
    productFlavors {
        development {
            dimension "mode"
            minSdkVersion 21
            resConfigs("ko", "xxhdpi")
            aaptOptions.cruncherEnabled = false
        }
        production {
            dimension "mode"
            minSdkVersion 16
        }
        original {
            buildConfigField "boolean", "SHOW_AD", "true"
            addManifestPlaceholders([APP_NAME: "@string/app_name"])
            dimension "edition"
        }
        pro {
            buildConfigField "boolean", "SHOW_AD", "false"
            addManifestPlaceholders([APP_NAME: "@string/app_name_pro"])
            applicationIdSuffix '.pro'
            dimension "edition"
        }
        free {
            buildConfigField "boolean", "SHOW_AD", "true"
            addManifestPlaceholders([APP_NAME: "@string/app_name"])
            applicationIdSuffix '.free'
            dimension "edition"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation files('src/main/libs/charset-detector.jar')
    implementation files('src/main/libs/chardet.jar')

    //encoding
    //implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    //ads는 항상 15.0.1을 유지할것
    implementation 'com.google.android.gms:play-services-ads:19.6.0'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'

    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-config:20.0.2'

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // Recommended: Add the Google Analytics SDK.
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    // Add the Firebase Crashlytics NDK dependency.
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.3.0'

//    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
//        transitive = true
//    }
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.multidex:multidex:2.0.1'
}

apply plugin: 'com.google.gms.google-services'
