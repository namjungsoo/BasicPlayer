apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.duongame.basicplayer'
        buildConfigField "boolean", "SHOW_AD", "true"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 10
        versionName "1.0.9"

        buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-16\""
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    sourceSets {
        main {
            // eclipse의 폴더명과 동일하게 사용함
            jniLibs.srcDirs = ["src/main/libs"]

            // android studio가 직접 컴파일하지 않게 함
            jni.srcDirs = []
        }
    }
    signingConfigs {
        release {
            storeFile file("../Mobicle2.keystore")
            storePassword "windbros"
            keyAlias "mobicle"
            keyPassword "windbros"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions("edition", "mode")
    productFlavors {
        development {
            dimension "mode"
            minSdkVersion 21
            resConfigs("ko", "xxhdpi")
            aaptOptions.cruncherEnabled = false
        }
        production {
            dimension "mode"
            minSdkVersion 14
        }
        original {
            buildConfigField "boolean", "SHOW_AD", "true"
            addManifestPlaceholders([APP_NAME: "@string/app_name"])
            dimension "edition"
        }
        pro {
            buildConfigField "boolean", "SHOW_AD", "false"
            addManifestPlaceholders([APP_NAME: "@string/app_name_pro"])
            applicationIdSuffix '.pro'
            dimension "edition"
        }
        free {
            buildConfigField "boolean", "SHOW_AD", "true"
            addManifestPlaceholders([APP_NAME: "@string/app_name"])
            applicationIdSuffix '.free'
            dimension "edition"
        }
    }
    // Android Oreo 이상에서는 64bit OS에서는 64bit so를 기본적으로 찾게되는데 이를 32bit so를 사용하라고 강제하는 명령임
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation files('src/main/libs/charset-detector.jar')
    implementation files('src/main/libs/chardet.jar')

    //encoding
    //implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    //ads는 항상 15.0.1을 유지할것
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.google.android.gms:play-services-analytics:16.0.4'

    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-config:16.1.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

// Add to the bottom of the file
//PRO
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
